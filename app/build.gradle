plugins {
    id 'com.android.application'
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.bruce.modulableapp"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    signingConfigs {
        release {//发布版本的签名配置
            storeFile file('D:/keys/imageim_mirror.jks')
            keyAlias "imageim"
            storePassword "imageim"
            keyPassword "imageim"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            // shrinkResources true //是否清理无用资源,依赖于minifyEnabled
            // zipAlignEnabled true //是否启用zipAlign压缩
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    if (variant.buildType.name.equals('release')) {
                        outputFileName = "YiMirror_${defaultConfig.versionName}_${releaseTime()}_release_ToB.apk"
                    } else if (variant.buildType.name.equals('debug')) {
                        outputFileName = "YiMirror_${defaultConfig.versionName}_${releaseTime()}_debug_ToB.apk"
                    }
                }
            }

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // 组件之间路由和通讯的ARooter
    implementation 'com.alibaba:arouter-api:1.5.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.1'
    // ButterKnife
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    // EventBus
    implementation 'org.greenrobot:eventbus:3.2.0'

    implementation project(':commonlib') // 通用公共库
    implementation project(':commonservice') // 本项目相关的公共服务库

    if(isNeedChat.toBoolean()) {
        implementation project(':chat')
    }
    if (isNeedHome.toBoolean()) {
        implementation project(':home')
    }
}